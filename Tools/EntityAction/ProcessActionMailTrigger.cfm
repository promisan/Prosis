
<!--- following actions are handled here

1. move to next step or generate the action
2. eMail action for user
3. eMail next actor
4. eMail next actor information to user
--->

<cfquery name="System" 
	 datasource="AppsSystem"
	 username="#SESSION.login#" 
	 password="#SESSION.dbpw#">
	 SELECT   *
	 FROM     Parameter	
</cfquery>  

<cfquery name="ThisAction" 
	 datasource="AppsOrganization"
	 username="#SESSION.login#" 
	 password="#SESSION.dbpw#">
	 SELECT * 
	 FROM   Ref_EntityActionPublish
	 WHERE  ActionCode      = '#Action.ActionCode#'
	 AND    ActionPublishNo = '#Action.ActionPublishNo#' 			 
</cfquery>

<!--- MS exchange ---------------------------- --->
<!--- CLOSE current action in MS Exchange task --->
<!--- ---------------------------------------- --->

<cfif System.ExchangeServer neq "">	
	
	<cfquery name="CloseTasks" 
		datasource="AppsOrganization"
		username="#SESSION.login#" 
		password="#SESSION.dbpw#">
			SELECT * 
			FROM   OrganizationObjectMail
			WHERE  ObjectId     = '#ObjectId#'
			AND    ActionCode   = '#Action.ActionCode#'
			AND    ExchangeId is not NULL
			AND    ActionStatus = 0
	</cfquery>		
				
	<cfif closetasks.recordcount gt "0">
		
		<cfoutput>
		
		   		<cfsavecontent variable="taskmessage">			
					  Action      : #Object.EntityDescription# #ThisAction.ActionDescription#					  
					  <p>
					  Reference   : #Object.ObjectReference2#					  
					  <p>
					  <cfif Form.actionStatus eq "2N">
					  Denied by : #SESSION.first# #SESSION.last# 
					  @ : #dateformat(now(),CLIENT.DateFormatShow)# #timeformat(now(),'HH:MM:SS')#
					  <cfelse>
					  Completed by : #SESSION.first# #SESSION.last# 
					  @ : #dateformat(now(),CLIENT.DateFormatShow)# #timeformat(now(),'HH:MM:SS')#
					  </cfif>		 				
				</cfsavecontent>
				
				<cfscript>
				    stask=StructNew();		
				  	stask.Message          = "#taskmessage#";									
					stask.Status           = "Completed";
				    stask.PercentCompleted = 100;											
				</cfscript> 	
				
		</cfoutput>
		
		<cfloop query="CloseTasks">
						
				<!--- ---------------------- --->	
				<!--- update exchange server --->
				<!--- ---------------------- --->									
					
				<cf_ExchangeTask
					account     = "#Account#"					
					action      = "delete"
					uid         = "#exchangeid#">  	
														
		</cfloop>	
				
		<!--- mark the tasks as closed in the table --->
		
		<cfquery name="CloseTasks" 
			datasource="AppsOrganization"
			username="#SESSION.login#" 
			password="#SESSION.dbpw#">
				UPDATE OrganizationObjectMail
				SET    ActionStatus = 1
				WHERE  ObjectId     = '#ObjectId#'
				AND    ExchangeId is not NULL
				AND    ActionCode   = '#Action.ActionCode#'
				AND    ActionStatus = 0
		</cfquery>	
		
	</cfif>

</cfif>	

<!--- MS exchange end of script -------------- --->
<!--- close current action in MS Exchange task --->
<!--- ---------------------------------------- --->	

<cfquery name="NextStep" 
	 datasource="AppsOrganization"
	 username="#SESSION.login#" 
	 password="#SESSION.dbpw#">
		 SELECT    TOP 1 A.ActionFlowOrder 
		 FROM      OrganizationObject O INNER JOIN
		           OrganizationObjectAction A ON O.ObjectId = A.ObjectId
		 WHERE     O.ObjectId = '#Object.Objectid#' 
		 AND       A.ActionStatus = '0' 
		 AND       O.Operational  = 1  
		 ORDER BY  A.ActionFlowOrder  
	 </cfquery>	
	 
 <!--- retrieve details beased on the defined step --->

     <cfquery name="NextAction" 
	 datasource="AppsOrganization"
	 username="#SESSION.login#" 
	 password="#SESSION.dbpw#">
	 SELECT    A.ActionCode, 
	           A.ActionPublishNo, 
			   A.OrgUnit, 
			   A.ActionFlowOrder,
			   A.ActionId
	 FROM      OrganizationObject O INNER JOIN
	           OrganizationObjectAction A ON O.ObjectId = A.ObjectId
	 WHERE     O.ObjectId        = '#Object.Objectid#' 
	 AND       A.ActionStatus    = '0' 
	 AND       A.ActionFlowOrder = '#NextStep.ActionFlowOrder#'
	 AND       O.Operational     = 1  	
	 </cfquery>
	
	 <!--- verify if next action requires email --->
	 
	 <cfif NextAction.recordcount gte "1">
	 		
		 <cfset delaymail = "0">
			 
	 <cfelse>
	 
	     <!--- -------------------------------------------------------------------------------------- --->
	     <!--- next has not been created, due eMail will thus be made as part of the creation process --->
		 <!--- -----------determine the next step that would be generated by ObjectStepAdd.cfm ------ --->
					
		 <cfset delaymail = "1">
			 		 
	</cfif>
				
	<cfquery name="Object" 
	 datasource="AppsOrganization"
	 username="#SESSION.login#" 
	 password="#SESSION.dbpw#">
		 SELECT O.*, 
		        R.*, 
				C.EnableEMail as ClassMail
		 FROM   OrganizationObject O, 
		        Ref_Entity R, 
			    Ref_EntityClass C
		 WHERE  Objectid       = '#Object.ObjectId#'
		 AND    O.EntityCode   = R.EntityCode
		 AND    O.EntityCode   = C.EntityCode 
		 AND    O.EntityClass  = C.EntityClass
		 AND    O.Operational  = 1
	</cfquery>
	
	<cfif Object.classMail   eq "1" and <!--- mail enabled for the flow --->
	      Object.enableEMail eq "1" and <!--- mail enabled fro the object --->
 		      (Form.actionStatus neq "0")>  <!--- user selected a forward, submit or deny --->
										
		<!--- 1. process personalised email defined for this ACTION to OTHER PARTIES --->
					
		<cfif ThisAction.PersonMailAction neq "">
			
			<!--- query potential document holder for eMail --->
			
			<cfquery name="CheckMail" 
			 datasource="AppsOrganization"
			 username="#SESSION.login#" 
			 password="#SESSION.dbpw#">
			 SELECT    P.ActionCode, 
			           M.*
		     FROM      Ref_EntityDocument M INNER JOIN
		               Ref_EntityActionPublish P ON M.DocumentCode = P.PersonMailAction
		     WHERE     P.ActionCode      = '#ThisAction.ActionCode#' 
			 AND       P.ActionPublishNo = '#Object.ActionPublishNo#'
			 AND       M.DocumentType    = 'mail'      
			 AND       M.EntityCode      = '#Object.EntityCode#'  
			</cfquery>
					
			
			<cfif CheckMail.recordcount eq "1">
									
				<cfparam name = "Form.SendTo"               default="">
				<cfparam name = "Form.SendCc"               default="">
				<cfparam name = "Form.SendBcc"              default="">
				<cfparam name = "Form.ActionMailFrom"       default="">	
				<cfparam name = "Form.ActionMailSubject"    default="">
				<cfparam name = "Form.ActionMailPriority"   default="">	
				<cfparam name = "Form.ActionMailBody"       default="">
				<cfparam name = "Form.ActionMailAttachment" default="99999">	
				
				<!--- this method passes default mail settings and has enough
				values to run a linked template to populate mail values
				to, subject, text, att --->
				
				<cfif Form.ActionMailPriority neq "0">
				
					<!--- #ThisAction.PersonMailObjectAttach# last field this is now shown for selection in the form --->

					<cf_ProcessMailHolder
						actionId     = "#Action.ActionId#"
						mailobject   = "#CheckMail.DocumentId#"					
						mailType     = "Holder"
						mailfrom     = "#Form.ActionMailFrom#"
						sendTo       = "#form.sendto#" 
						sendCc       = "#form.sendcc#" 
						sendBcc      = "#form.sendbcc#" 
						sendSubject  = "#Form.ActionMailSubject#"	
						sendPriority = "#Form.ActionMailPriority#"							
						sendText     = "#Form.ActionMailBody#"
						sendAtt      = "#Form.ActionMailAttachment#"
						sendAttDoc   = "#ThisAction.PersonMailActionAttach#"
						sendAttObj   = "#ThisAction.PersonMailObjectAttach#">	
					
				</cfif>							
							
			</cfif> 
	
	    </cfif>		
						
		<cfif delaymail eq "0">	
		
		
			<!--- 2. send email to OTHER PARTIES - DUE after this step --->

			 <cfloop query="NextAction">	
		   
		   	    <cfquery name="NextCheck" 
				 datasource="AppsOrganization"
				 username="#SESSION.login#" 
				 password="#SESSION.dbpw#">
				 SELECT    TOP 1 *
				 FROM      Ref_EntityActionPublish
				 WHERE     ActionPublishNo = '#ActionPublishNo#' 
				 AND       ActionCode      = '#ActionCode#' 
				 ORDER bY  Created DESC 
			    </cfquery>	
																		
			   	<cfif NextCheck.PersonMailCode neq "">
						
					<!--- query potential document holder for eMail for next step --->
					
					<cfquery name="CheckMail" 
					 datasource="AppsOrganization"
					 username="#SESSION.login#" 
					 password="#SESSION.dbpw#">
					 SELECT    P.ActionCode, M.*
				     FROM      Ref_EntityDocument M INNER JOIN
				               Ref_EntityActionPublish P ON M.DocumentCode = P.PersonMailCode
				     WHERE     M.DocumentType    = 'mail' 
					 AND       M.EntityCode      = '#Object.EntityCode#'
					 AND       P.ActionPublishNo = '#Object.ActionPublishNo#'
				     AND       P.ActionCode      = '#NextCheck.ActionCode#'   
					</cfquery>
																							
					<cfif CheckMail.recordcount gte "1">
					
						<cfparam name = "Form.ActionMailSubject"    default="">
						<cfparam name = "Form.ActionMailBody"       default="">
						<cfparam name = "Form.ActionMailAttachment" default="99999">	
						<cfparam name = "Form.SendTo"               default="">
						<cfparam name = "Form.SendCc"               default="">
						<cfparam name = "Form.SendBcc"              default="">
						
						<cf_ProcessMailHolder
							actionId    = "#ActionId#"
							mailobject  = "#CheckMail.DocumentId#"		
							mailType    = "Due"		
							mailfrom    = "#Form.ActionMailFrom#"							 	
							sendTo      = "#form.sendto#" 
							sendCc      = "#form.sendcc#" 
							sendBcc     = "#form.sendbcc#" 
							sendSubject = "#Form.ActionMailSubject#"
							sendText    = "#Form.ActionMailBody#"
							sendAtt     = "#Form.ActionMailAttachment#"
							sendAttDoc  = "0"
							sendAttObj  = "0">
								
							<!--- attachment feature not enabled for this yet : hanno --->
													   
					</cfif>
					
			
			   </cfif>
			 		   
		       <!--- 3. NOTIFICATION email to ACTOR for next step --->	
			
			   			   		   				      			
			   <cfif NextCheck.EnableNotification eq "1" and NextCheck.recordcount eq "1">
			   							
					 <cfset actionId = NextAction.ActionId>	 												 
					
					 <cfif NextCheck.NotificationManual eq "0" 
					        <!--- or NextCheck.DueMailCode neq ""  --->
							or NextAction.recordcount gt "1">		<!--- do not allow dialog if > 1 record --->							
					 	 					 					 
							<cfinclude template="ProcessMail.cfm">	
												
					 <cfelse>					  
					 					 					 	
						 <cfoutput>
							 <script>
							 	try {
							   		maildialog('#object.objectid#','#NextAction.ActionCode#','#NextCheck.NotificationGlobal#')
							   	} catch(e) {}					
							 </script>
						 </cfoutput>	 
							
						 <!--- <cfexit method="EXITTEMPLATE"> --->
					 
					 </cfif>						 				 
						 
			   </cfif> 
		   
		   </cfloop>	
		   		   
	   </cfif>   
	  		
	</cfif>	